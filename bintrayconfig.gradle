if (!project.rootProject.file('local.properties').exists()) {
  println "Not applying install.gradle"
  return
}

if (shard == null) {
  throw new IllegalStateException("Must specify a shard in module's build.gradle")
} else if (versions == null || versions.publishVersion == null) {
  throw new IllegalStateException("Unable to reference publishVersion")
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

String publishUser = properties.getProperty("bintray.user")
String publishKey = properties.getProperty("bintray.apikey")

if (publishUser == null || publishUser.isEmpty() ||
    publishKey == null || publishKey.isEmpty()) {
  throw new IllegalStateException("Must specify Bintray user/API key in your local.properties.")
}

publish {
  bintrayUser = publishUser
  bintrayKey = publishKey
  userOrg = 'drummer-aidan'
  groupId = 'com.afollestad.material-dialogs'
  uploadName = "material-dialogs:$shard"
  artifactId = shard
  publishVersion = versions.publishVersion
  desc = 'A beautiful, fluid, and customizable dialogs API.'
  website = 'https://github.com/afollestad/material-dialogs'
  dryRun = false
}